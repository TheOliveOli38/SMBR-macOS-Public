class_name Burner
extends AnimatableBody2D

@export_enum("Up", "Down", "Left", "Right") var direction := 0

var can_burn := true

func _ready() -> void:
	if $SignalExposer.total_inputs <= 0:
		$Timer.start()

func do_cycle() -> void:
	if can_burn == false:
		return
	can_burn = false
	if BooRaceHandler.countdown_active == false:
		if $OnScreen.is_on_screen():
			AudioManager.play_sfx("burner", global_position)
		do_animation()
		await get_tree().create_timer(0.25, false).timeout
		%Shape.set_deferred("disabled", false)
		await get_tree().create_timer(1.5, false).timeout
	%Shape.set_deferred("disabled", true)
	can_burn = true
	if $SignalExposer.total_inputs <= 0:
		$Timer.start()

func do_animation() -> void:
	%Flame.show()
	%Flame.play("Rise")
	await %Flame.animation_finished
	%Flame.play("Loop")
	await get_tree().create_timer(1, false).timeout
	%Flame.play("Fall")
	await %Flame.animation_finished
	%Flame.hide()

func damage_player(player: Player, type: String = "Normal") -> void:
	player.damage(type if type != "Normal" else "")
